plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4' apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'

    group = 'com.dfac'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'io.grpc:grpc-netty-shaded:1.60.0'
        implementation 'io.grpc:grpc-protobuf:1.60.0'
        implementation 'io.grpc:grpc-stub:1.60.0'
        compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
        
        implementation 'org.apache.curator:curator-framework:5.5.0'
        implementation 'org.apache.curator:curator-recipes:5.5.0'
        
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.25.1"
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.60.0'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}
